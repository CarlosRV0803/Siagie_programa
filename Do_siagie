/*==============================================================================

Proyecto: Limpieza de Siagie de colegios públicos
Autor: Racio-Carlos
Fecha de modificación: 21/01/2020 

==============================================================================*/

global siagie2020 "C:\Users\analistaup12\Desktop\MINEDU\Siagie"


/*program drop bases
program define bases
forvalues x=2015/2019{
set excelxlsxlargefile on
import excel "$siagie2020\Matrícuila_SIAGIE_2015_2019.xlsx", clear first sheet(`x')
save "$siagie2020\Siagie_`x'", replace
}
end
*/

*=========================
*IDENTIFICACION VARIABLES DESPLAZADAS
*==========================
forvalues y=2015/2019{
use "$siagie2020\Siagie_`y'.dta", clear
ren *,l
gen aux=1 if turno==""
cap replace total = ab if aux==1
drop aux
save "$siagie2020\Siagie_`y'_temp.dta", replace
}

*=========================
*RESHAPE PARA CADA BASE
*=========================
forvalues y=2015/2019{
use "$siagie2020\Siagie_`y'_temp.dta", clear
*Se eliminan servicios que no se encuentran en E.B.R.
drop if strpos(dsc_nivel,"Especial") | strpos(dsc_nivel,"Programa") 
*Se renombra variables
ren cod_local codlocal
destring id_anio, replace

foreach x in cod_dre cod_ugel ubigeo codlocal cod_mod {	
	tostring `x', replace 		
}
*Se agrega cantidad de digitos segun variable de interes
replace cod_dre = "0"*(4-length(cod_dre))+cod_dre
replace cod_ugel = "0"*(4-length(cod_ugel))+cod_ugel
replace ubigeo = "0"*(6-length(ubigeo))+ubigeo
replace codlocal = "0"*(6-length(codlocal))+codlocal
replace cod_mod = "0"*(7-length(cod_mod))+cod_mod

destring total, replace
duplicates tag cod_mod anexo id_grado id_seccion, g(du_`y')

g grado=.
replace grado=1 if strpos(dsc_grado,"2") & strpos(dsc_nivel,"Inicial")
replace grado=2 if strpos(dsc_grado,"3") & strpos(dsc_nivel,"Inicial")
replace grado=3 if strpos(dsc_grado,"4") & strpos(dsc_nivel,"Inicial")
replace grado=4 if strpos(dsc_grado,"5") & strpos(dsc_nivel,"Inicial")

replace grado=5 if strpos(dsc_grado,"PRIMERO") & strpos(dsc_nivel,"Primaria")
replace grado=6 if strpos(dsc_grado,"SEGUNDO") & strpos(dsc_nivel,"Primaria")
replace grado=7 if strpos(dsc_grado,"TERCERO") & strpos(dsc_nivel,"Primaria")
replace grado=8 if strpos(dsc_grado,"CUARTO") & strpos(dsc_nivel,"Primaria")
replace grado=9 if strpos(dsc_grado,"QUINTO") & strpos(dsc_nivel,"Primaria")
replace grado=10 if strpos(dsc_grado,"SEXTO") & strpos(dsc_nivel,"Primaria")

replace grado=11 if strpos(dsc_grado,"PRIMERO") & strpos(dsc_nivel,"Secundaria")
replace grado=12 if strpos(dsc_grado,"SEGUNDO") & strpos(dsc_nivel,"Secundaria")
replace grado=13 if strpos(dsc_grado,"TERCERO") & strpos(dsc_nivel,"Secundaria")
replace grado=14 if strpos(dsc_grado,"CUARTO") & strpos(dsc_nivel,"Secundaria")
replace grado=15 if strpos(dsc_grado,"QUINTO") & strpos(dsc_nivel,"Secundaria")

gen nivel_servicio = "inicial" if grado>=1 & grado<=4
replace nivel_servicio = "primaria" if grado>=5 & grado<=10
replace nivel_servicio = "secundaria" if grado>=11

reshape wide total,i(cod_mod anexo id_grado id_seccion nivel_servicio) j(grado)

gen cant0_alum = total1
egen cant1_alum = rowtotal(total2 total5 total11)
egen cant2_alum = rowtotal(total3 total6 total12)
egen cant3_alum = rowtotal(total4 total7 total13)
egen cant4_alum = rowtotal(total8 total14)
egen cant5_alum = rowtotal(total9 total15)
gen cant6_alum = total10
egen cant_total = rowtotal(cant*)
destring id_seccion, replace
replace id_seccion=1 if id_seccion==.

gen num_secc=1

collapse (firstnm) id_anio ubigeo cod_dre nom_dre cod_ugel nom_ugel departamento provincia distrito centro_poblado estadoescale gestion (sum) cant* num_secc , by(codlocal cod_mod anexo nivel_servicio)

label var id_anio "año"
label var ubigeo "ubigeo"
label var cod_dre "codigo dre"
label var nom_dre "nombre DRE"
label var cod_ugel "codigo ugel"
label var nom_ugel "nombre UGEL"
label var departamento "departamento"
label var provincia "provincia"
label var distrito "distrito"
label var centro_poblado "centro poblado"
label var codlocal "codigo local"
label var cod_mod "codigo modular"
label var anexo "anexo"
label var estadoescale "estado escale"
label var num_secc "número de secciones"
label var nivel_servicio "nivel de servicio"
save "$siagie2020\Siagie_`y'_temp.dta", replace
}

*=========================
*APPEND BASES
*=========================
use "$siagie2020\Siagie_2015_temp.dta", clear
forvalues y=2016/2019{
append using "$siagie2020\Siagie_`y'_temp.dta"
}
compress
save "$siagie2020\Siagie_apilado_2015_2019.dta", replace
